(define (init-markvector N)
  (assert (fx> N 1))
  (let ((M (make-bytevector N 1)))
    (bytevector-u8-set! M 0 0)
    (bytevector-u8-set! M 1 0)
    M))

(define (marks-limit V N) (fxmin N (bytevector-length V)))

(define (sieve! M N p cursor)
  (let loop ((c cursor))
    (if (fx< c N)
        (begin (bytevector-u8-set! M c 0)
               (loop (fx+ c p)))
        (fx- c N))))

(define (next-prime-offset M N start)
  (let loop ((i (fx+ start 1 (fxlogand start 1))))
    (if (and (fx< i N)
             (fxzero? (bytevector-u8-ref M i)))
        (loop (fx+ 2 i))
        i)))

(define (sum V N)
  (let loop ((i 0) (s 0))
    (if (fx< i N)
        (loop (fx1+ i) (fx+ s (bytevector-u8-ref V i)))
        s)))

(define (compactify primes cursors n)
  (let ((prime-vector (make-fxvector n))
        (cursor-vector (make-fxvector n)))
    (let loop ((i (fx1- n))
               (P primes)
               (C cursors))
      (if (pair? P)
        (begin (fxvector-set! prime-vector i (car P))
               (fxvector-set! cursor-vector i (car C))
               (loop (fx1- i) (cdr P) (cdr C)))
        (values prime-vector cursor-vector)))))

(define (optimus-primes N)
  (let ((M ((if (fx< N 2) make-bytevector init-markvector) N)))
    (let loop ((p 2)
               (n 0)
               (P '())
               (C '()))
      (if (fx< p N)
        (loop (next-prime-offset M N p)
              (fx1+ n)
              (cons p P)
              (cons (sieve! M N p (+ p p)) C))
        (let-values (((primes cursors) (compactify P C n)))
          (values M primes cursors))))))

(define (sieve-recursor-count! N M P C)
  (bytevector-fill! M 1)
  (let ((l (fxvector-length P)))
    (let loop ((i 0))
      (if (fx< i l)
        (let ((p (fxvector-ref P i))
              (c (fxvector-ref C i)))
          (fxvector-set! C i (sieve! M N p c))
          (loop (fx1+ i)))
        (sum M N)))))

(define (go N)
  (let ((L (fx1+ (isqrt N))))
    (let-values (((M P C) (optimus-primes L)))
      (if (fxzero? (fxvector-length P))
          0
          (let loop ((n (fxvector-length P))
                     (l (fx- (fx1+ N) L)))
            (if (fxpositive? l)
                (loop (fx+ n (sieve-recursor-count! (fxmin L l) M P C)) (fx- l L))
                n))))))

(display (go (string->number (cadr (command-line))))) (newline)
